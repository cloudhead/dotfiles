# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=st
pkgver=0.7
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X.'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(http://dl.suckless.org/st/$pkgname-$pkgver.tar.gz
       config.h
       st-scrollback-20160727-308bfbf.diff)

md5sums=('29b2a599cf1511c8062ed8f025c84c63'
         '4e1c7c6c9829e72719365a8cd30bddd4'
         'c55076d94247e121a1a86d32a793cf2e')

prepare() {
  cd $srcdir/$pkgname-$pkgver
  # skip terminfo which conflicts with ncurses
  sed -i '/\@tic /d' Makefile
  cp $srcdir/config.h config.h
  patch -Np1 < ../st-scrollback-20160727-308bfbf.diff
}

build() {
  cd $srcdir/$pkgname-$pkgver
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/$pkgname-$pkgver
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
